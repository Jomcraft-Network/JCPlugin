buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'net.darkhax.curseforgegradle' version '1.0.11'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'

archivesBaseName = "${rootProject.name}"

ext.replace_version(version)

version = "1.16.x-${version}-Forge"

minecraft {
    mappings channel: 'official', version: '1.16.5'

    runs {
        client {
            workingDirectory file('./run')
            property 'forge.logging.markers', 'REGISTRIES'
            ideaModule "JCPlugin.Forge-1.16.5.main"
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config="+"jcplugin.mixin.json"

            mods {
                "${rootProject.name}" {
                    source sourceSets.main
                    source project(":Core").sourceSets.main
                }
            }
        }

        server {
            workingDirectory file('./run')
            property 'forge.logging.markers', 'REGISTRIES'
            ideaModule "JCPlugin.Forge-1.16.5.main"
            property 'forge.logging.console.level', 'debug'

            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config="+"jcplugin.mixin.json"

            mods {
                "${rootProject.name}" {
                    source sourceSets.main
                    source project(":Core").sourceSets.main
                }
            }
        }
    }
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

mixin {
    add sourceSets.main, "jcplugin.refmap.json"
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.39'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    testAnnotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation project(":Core")
}

/*processResources {
    duplicatesStrategy = 'include'

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        //	expand 'forge_version': forge_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}*/

jar {

    manifest {
        attributes([
                "MixinConfigs" : "jcplugin.mixin.json"
        ])
    }

}

tasks.withType(JavaCompile) {
    source(project(":Core").sourceSets.main.allSource)
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    if (System.getenv("CURSE_API") != null && !System.getenv("CURSE_API").equals("")) {
        disableVersionDetection()
        apiToken = System.getenv("CURSE_API")

        def mainFile = upload(659192, jar)
        mainFile.releaseType = 'beta'
        mainFile.displayName = "$archivesBaseName-$version"
        mainFile.changelogType = 'html'
        mainFile.changelog = file('../../changelog.html')
        mainFile.addModLoader('Forge')
        mainFile.addJavaVersion('Java 8')
        mainFile.addGameVersion('1.16.5')

        def sourcesFile = mainFile.withAdditionalFile(sourcesJar)
        sourcesFile.changelog = file('../../changelog.html')
        sourcesFile.displayName = "$archivesBaseName-$version-sources"
    }
}

task("copyRelease") {
    dependsOn "build"

    doFirst {
        println "Gathering builds"
        copy {
            def libDir = project.projectDir.toPath().resolve("build/libs")
            from(libDir) {
                include "*.jar"
                exclude "*-dev.jar", "*-sources.jar"
            }
            into "../../build/libs/"
            duplicatesStrategy DuplicatesStrategy.INCLUDE
        }
    }
}